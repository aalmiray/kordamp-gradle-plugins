
[[_authoring_]]
= Authoring

Kordamp offers a set of additional features for plugin and build authors.

== Properties

In the Maven world, plugins define parameters for their inputs using a `@Parameter` annotation that exposes the annotated
element (typically a field) in such a way that its value can be defined using project or System properties. This allows
consumer to define and override values as needed. For example the `EchoMojo` from the
link:https://github.com/Ekryd/echo-maven-plugin/[echo-maven-plugin]

[source,java]
.EchoMojo.java
----
class EchoMojo extends AbstractMojo {
    /**
     * The message text that should be echoed
     */
    @Parameter(property = "echo.message")
    private String message;

    ...
}
----

The value of the `message` parameter can be defined inside a pom.xml like so

[source,xml]
[subs="verbatim"]
----
<project ...>
    <groupId>com.acme</groupId>
    <artifactId>sample</artifactId>
    <version>0.0.0-SNAPSHOT</version>

    <properties>
        <echo.message>Hello World</echo.message>
    </properties>
</project>
----

Or it could be passed in the command line like so

[source]
----
$ gm com.github.ekryd.echo-maven-plugin:echo-maven-plugin:1.2.0:echo -Decho.message="Hello from command"
----

The `org.kordamp.gradle.property` package provides classes that deliver similar functionality, following a pattern of
using a `org.gradle.api.provider.Property` for write and a `org.gradle.api.provider.Provider` for read. For example, the
link:api/org/kordamp/gradle/property/SimpleStringState.html[`SimpleStringState`] can be used to replicate the behavior of
the `EchoMojo` but using a Gradle task

[source,groovy]
.EchoTask.groovy
----
package com.acme

import groovy.transform.CompileStatic
import org.gradle.api.DefaultTask
import org.gradle.api.provider.Property
import org.gradle.api.provider.Provider
import org.gradle.api.tasks.Input
import org.gradle.api.tasks.Internal
import org.gradle.api.tasks.Optional
import org.gradle.api.tasks.TaskAction
import org.gradle.api.tasks.options.Option
import org.kordamp.gradle.property.SimpleStringState
import org.kordamp.gradle.property.StringState

@CompileStatic
class EchoTask extends DefaultTask {
    private final StringState message

    EchoTask() {
        message = SimpleStringState.of(this, 'echo.message', 'default message')
    }

    @Option(option='echo-message', description = 'The message to write')
    void setMessage(String message) {
        getMessage().set(message)
    }

    @Internal
    Property<String> getMessage() {
        message.property
    }

    @Input
    @Optional
    Provider<String> getResolvedMessage() {
        message.provider
    }

    @TaskAction
    void echo() {
        println getResolvedMessage().get()
    }
}
----

Take note that the task action reads the value using the `Provider` field. you're free to mutate (write) the value as
much as needed using the `Property` field but make sue to always use the `Provider` for reading. This task can be applied
to a build as you normally would

[source,groovy]
.build.gradle
----
task echo(type: com.acme.EchoTask) {

}
----

Invoking the `echo` task with no parameters nor explicit value for the `message` property in the build yields the following

[source]
----
$ gm echo

> Task :echo
default message
----

Defining a project property, whether it's inline with `-P` at the command line, in a `gradle.properties` file located at
the project directory or your home directory, or using a gradle init script, would result in that value being picked up
instead

[source]
----
$ gm echo -Pecho.message="from project"

> Task :echo
from project
----

Passing a System property, in any way a System property can be defined, results in that value being chosen this time around

[source]
----
$ gm echo -Pecho.message="from project" -Decho.message="from System"

> Task :echo
from System
----

If there's an environment variable whose key name follows a naming convention (all caps, substitute `.` for `_`) then its
value will be the selected one

[source]
----
$ export ECHO_MESSAGE="from environment"
$ gm echo -Pecho.message="from project" -Decho.message="from System"

> Task :echo
from environment
----

But if a specific value is set in the task's `message` property then and only then it will be picked up

[source,groovy]
.build.gradle
----
task echo(type: com.acme.EchoTask) {
    message = 'from build'
}
----

[source]
----
$ gm echo -Pecho.message="from project" -Decho.message="from System"

> Task :echo
from build
----

Explicit values set in the build file have priority over values provided via environment, System, or project properties.
You can switch this behavior (globally) by setting a System property `kordamp.property.priority` to any of these values

 * `PROPERTY`
 * `PROVIDER`

The resolution order shown here is ENV_SYS_PROP, but it can be changed to any of these values

 * `ENV_SYS_PROP`
 * `ENV_PROP_SYS`
 * `SYS_ENV_PROP`
 * `SYS_PROP_ENV`
 * `PROP_ENV_SYS`
 * `PROP_SYS_ENV`

Also, when running in a multi-project build you may want to provide default values depending on context, such as the
path of the owning task (typically of the format `:project:taskname`), or the owning project (`:project`) or global to
the whole build. These previous elements will be prefixed to the property/variable name with the default setting being
`GLOBAL_PROJECT_OWNER` which means all prefixes will be checked, from more specific (`OWNER`) to least (`GLOBAL`). These
are the values you can choose from

 * `GLOBAL_PROJECT_OWNER`
 * `GLOBAL_PROJECT`
 * `GLOBAL_OWNER`
 * `GLOBAL`
 * `PROJECT_OWNER`
 * `PROJECT`
 * `OWNER`

Any `:` found in the owner's or project's path will be turned into a `.`, except for the first which will be removed
altogether, thus if the `echo` task is defined on a project  whose path is `:project` the following property names will
be checked in turn

 * `project1.echo.echo.message`
 * `project1.echo.message`
 * `echo.message`

You may fix these values when authoring your own tasks or you may leave it to the consumer to define the order. Kordamp
will check the following System properties

[horizontal]
kordamp.property.order:: Determines the order between ENV, SYS, and PROP. Defaults to `ENV_SYS_PROP`.
kordamp.property.access:: Determines the access level for calculating a prefix. Defaults to `GLOBAL_PROJECT_OWNER`.
kordamp.property.priority:: Determines the priority between explicit values in the build file (PROPERTY) or values resolved via
environment, System, or project properties (PROVIDER). Defaults to `PROPERTY`.

There are base states for the following types:

 * `link:api/org/kordamp/gradle/property/SimpleBooleanState.html[BooleanState]`
 * `link:api/org/kordamp/gradle/property/SimpleDirectoryState.html[DirectoryState]`
 * `link:api/org/kordamp/gradle/property/SimpleIntegerState.html[IntegerState]`
 * `link:api/org/kordamp/gradle/property/SimpleListState.html[ListState]`
 * `link:api/org/kordamp/gradle/property/SimpleLongState.html[LongState]`
 * `link:api/org/kordamp/gradle/property/SimpleMapState.html[MapState]`
 * `link:api/org/kordamp/gradle/property/SimpleRegularFileState.html[RegularFileState]`
 * `link:api/org/kordamp/gradle/property/SimpleSetState.html[SetState]`
 * `link:api/org/kordamp/gradle/property/SimpleListState.html[StringState]`

== File Cache

A File cache local to a Gradle distribution is also accessible for storing data between builds. The cache is provided by
the `org.kordamp.gradle.util.Cache` class and exposes the following behavior:

boolean has(Gradle gradle, Key key):: Queries the existence of a key in the cache.
long lastModified(Gradle gradle, Key key):: Returns the last modification timestamp of the given key.
long touch(Gradle gradle, Key key):: Updates the last modification timestamp of the given key.
void delete(Gradle gradle, Key key):: Deletes an entry from the cache.
boolean get(Gradle gradle, Key key, Consumer<? super InputStream> consumer):: Reads from the cache using an {@code java.io.InputStream}.
boolean read(Gradle gradle, Key key, Consumer<? super BufferedReader> consumer):: Reads from the cache using a {@code java.io.BufferedReader}.
boolean put(Gradle gradle, Key key, Consumer<? super OutputStream> consumer):: Writes to the cache using an {@code java.io.OutputStream}.
boolean write(Gradle gradle, Key key, Consumer<? super BufferedWriter> consumer):: Writes to the cache using a {@code java.io.BufferedWriter}.
void clear(Gradle gradle):: Deletes the whole cache.
Key key(Object input):: Creates a `Key` instance based on the given input.

Example:

[source,groovy]
----
// obtain a key
Cache.Key key = Cache.key(settings.gradle.gradleHomeDir.absolutePath + '-plugins.txt')

// define a reader function
Consumer<InputStream> reader = { InputStream r -> r.text.split('\n').each { pluginId -> corePlugins.add(pluginId) } }

// read from the cache
if (!Cache.getInstance().get(settings.gradle, key, reader)) {
    // read failed, compute values
    computeDefaultPlugins()
    // write to the cache
    Cache.getInstance().write(settings.gradle, key) { w -> w.write(corePlugins.join('\n')) }
}
----

